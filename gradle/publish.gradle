import groovy.json.JsonSlurper

def publishConf = new JsonSlurper().parseText(rootProject.file("publish.json").text)

// usage: "gradlew -PmavenURL=/path [-PmavenUser={user}] -PmavenAccessToken={token} :{module}:clean :{module}:publish"
def mavenURL = hasProperty('mavenURL') ? getProperty('mavenURL') : "https://pkgs.dev.azure.com/foodtruckinc/Wonder/_packaging/maven-local/maven/v1"
def mavenPass = hasProperty('mavenAccessToken') ? getProperty('mavenAccessToken') : System.getenv('MAVEN_ACCESS_TOKEN')

publishConf.modules.each { module ->
    project(module.name) {
        apply plugin: 'maven-publish'

        task sourceJar(type: Jar) {
            from sourceSets.main.allJava
            archiveClassifier = "sources"
        }

        publishing {
            publications {
                apiInterface(MavenPublication) {
                    groupId 'com.wonder'
                    artifactId module.artifactId
                    version module.version
                    from components.java
                    artifact sourceJar
                }
            }
            repositories {
                maven {
                    url mavenURL
                    credentials {
                        username = 'foodtruckinc'
                        password = mavenPass
                    }
                }
            }
        }
    }
}
